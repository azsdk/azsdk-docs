### Contents

- [Overview](#overview)
- [Usage statistics](#usage-statistics) 
    - [Subscriptions evaluated by AzSDK](#subscriptions-evaluated-by-azsdk)
    - [Resources evaluated by AzSDK](#resources-evaluated-by-azsdk)
    - [AzSDK Version statistics](#azsdk-version-statistics)
- [Controls statistics](#controls-statistics)
    - [Controls scan result distribution on daily basis](#controls-scan-result-distribution-on-daily-basis)
    - [Drift in failing controls](#drift-in-failing-controls)
    - [Drift in attested controls](#drift-in-attested-controls)
    
## Overview

The telemetry data can be leveraged by subscription owners to understand AzSDK usage, monitor compliance, troubleshooting etc. using querying capabilities provided by App Insights.

## Usage statistics

### Subscriptions evaluated by AzSDK
Below query is useful to get an idea of overall AzSDK usage in your subscriptions. It displays number of subscriptions scanned by AzSDK (through CA runbook/SpotCheck (Adhoc scan)/VSO task) in last 7 days. 

``` AIQL
customEvents
| where timestamp >= ago(7d)
| where name == "Control Scanned"
| summarize count() by Day = bin(timestamp,1d), ScanSource = tostring(customDimensions.ScanSource),tostring(customDimensions.ResourceId) 
| summarize ResourceCount = count() by Day, ScanSource
| render barchart
```

### Resources evaluated by AzSDK
Below query is useful to get an idea of overall AzSDK usage in your subscriptions. It displays number of resources scanned by AzSDK (through CA runbook/SpotCheck (Adhoc scan)/VSO task) in last 7 days.  

``` AIQL
customEvents
| where timestamp >= ago(7d)
| where name == "Control
Scanned"
| summarize count() by Day = bin(timestamp,1d), ScanSource = tostring(customDimensions.ScanSource),tostring(customDimensions.SubscriptionId), tostring(customDimensions.ResourceId) 
| summarize count() by Day, ScanSource
| render barchart
```
**Below is the sample graph generated by App Insights for above two queries.**

![AIGraph_AzSDK_Usage.jpg](/Images/06_AIGraph_AzSDK_Usage.jpg)

### AzSDK Version statistics

#### _CA AzSDK module version usage_

Below query is useful to check AzSDK module version used by CA installed in subscriptions.

``` AIQL
customEvents
| where timestamp >= ago(3d)
| where name == "Control Scanned"
| where customDimensions.ScanSource =="Runbook"
| summarize ControlsScanned = count() by Version = tostring(customDimensions.ScannerVersion), SubId = tostring(customDimensions.SubscriptionId)
| distinct Version,SubId,ControlsScanned
| summarize CurrentRunningCAAzSDKVersion=count() by Version
| render piechart
```

#### _SpotCheck AzSDK module version usage_

Below query is useful to check AzSDK module version used by developers in local scan.

``` AIQL
customEvents
| where timestamp >= ago(3d)
| where name == "Control Scanned"
| where customDimensions.ScanSource =="SpotCheck"
| summarize ControlsScanned = count() by Version = tostring(customDimensions.ScannerVersion), SubId = tostring(customDimensions.SubscriptionId)
| distinct Version,SubId,ControlsScanned
| summarize AzSDKVersion=count() by Version
| render piechart
```

#### _CA runbook version usage_

Below query is useful to check CA runbook version in your subscriptions.

``` AIQL
customEvents
| where timestamp > ago(2d) and name == "Control Scanned" and customDimensions.ScanSource == "Runbook"
| summarize arg_max(timestamp, *) by tostring(customDimensions.SubscriptionId)
| project tostring(customDimensions.SubscriptionId)
| join kind= leftouter
(
customEvents
| where timestamp > ago(2d) and name == "CA Job Started"
| summarize arg_max(timestamp, *) by tostring(customDimensions.SubscriptionId)
| project tostring(customDimensions.SubscriptionId),tostring(customDimensions.AzSDKRunbookVersion)
)
on customDimensions_SubscriptionId
| extend RunbookVersion = iff(isempty(customDimensions_AzSDKRunbookVersion)==true,"< 2.5.0",customDimensions_AzSDKRunbookVersion)
| summarize RunningRunbook=count() by RunbookVersion
| render piechart
```
**Below are the sample graphs generated by App Insights for above three queries.**

![AIGraph_AzSDK_ModuleVersion.jpg](/Images/06_AIGraph_AzSDK_ModuleVersion.jpg)

![AIGraph_AzSDK_RunbookVersion.jpg](/Images/06_AIGraph_AzSDK_RunbookVersion.jpg)


## Controls statistics
### _Controls scan result distribution on daily basis_

Below query is useful to get an idea of overall scan happening in all subscriptions.

``` AIQL
customEvents
| where timestamp >= ago(7d)
| where name == "Control Scanned"
| summarize count() by bin(timestamp, 1d), tostring(customDimensions.ResourceId), VerificationResult = tostring(customDimensions.VerificationResult) 
| summarize count() by Day = bin(timestamp, 1d), VerificationResult 
| render barchart
```

**Below is the sample graph generated by App Insights for above query.**
![AIGraph_AzSDK_ScanResultDistribution.jpg](/Images/06_AIGraph_AzSDK_ScanResultDistribution.jpg)

### _Drift in failing controls_

You can monitor the increase/decrease in "Failed" controls in last two days with this query. 
Positive drift needs your attention. Examine the resources whose controls are showing positive drift. Negative drift shows that controls are being fixed in last two days which is positive sign. 

``` AIQL
let combinedResult = customEvents
| where timestamp < ago(2d) and timestamp >= ago(4d)
| where  customDimensions.ScanSource =="Runbook" and name == "Control Scanned"
| summarize arg_max(timestamp, *) by tostring(customDimensions.SubscriptionId), tostring(customDimensions.SubscriptionName), tostring(customDimensions.ControlId)
| project tostring(customDimensions.SubscriptionId), tostring(customDimensions.SubscriptionName),tostring(customDimensions.ResourceId), tostring(customDimensions.ControlId), oldresult =tostring(customDimensions.VerificationResult)
| join
(
    customEvents
    | where timestamp >= ago(2d)
    | where  customDimensions.ScanSource =="Runbook" and name == "Control Scanned"
    | summarize arg_max(timestamp, *) by tostring(customDimensions.SubscriptionId), tostring(customDimensions.SubscriptionName), tostring(customDimensions.ControlId)
    | project tostring(customDimensions.SubscriptionId), tostring(customDimensions.SubscriptionName),tostring(customDimensions.ResourceId), tostring(customDimensions.ControlId), newresult = tostring(customDimensions.VerificationResult)
)
on customDimensions_SubscriptionId, customDimensions_SubscriptionName,customDimensions_ResourceId, customDimensions_ControlId
| project tostring(customDimensions_SubscriptionId), tostring(customDimensions_SubscriptionName),tostring(customDimensions_ResourceId), tostring(customDimensions_ControlId),oldresult,newresult;

let oldScan = combinedResult
| where oldresult == "Failed"
| summarize oldScanCount = count() by tostring(customDimensions_ControlId);

let newScan = combinedResult
| where newresult == "Failed"
| summarize newScanCount = count() by tostring(customDimensions_ControlId);

oldScan
| join
(
    newScan
)
on customDimensions_ControlId
| project ControlId=tostring(customDimensions_ControlId),BeforeCount=oldScanCount,CurrentCount=newScanCount
| where BeforeCount != CurrentCount
| extend Drift =CurrentCount-BeforeCount
| order by Drift desc
| project ControlId,BeforeCount,CurrentCount,Drift
| project ControlId, BeforeCount, CurrentCount, Drift
```

#### _Drift in attested controls_

You can monitor the increase/decrease in "Attested" controls in last two days with this query. 
Positive drift needs your attention. Find out the reason behind attesting controls instead of fixing them. Best security practice is to fix the controls rather than attesting. 

``` AIQL
let combinedResult = customEvents
| where timestamp > now(-4d) and timestamp <= now(-2d)
| where  customDimensions.ScanSource =="Runbook" and name == "Control Scanned"
| summarize arg_max(timestamp, *) by tostring(customDimensions.SubscriptionId), tostring(customDimensions.SubscriptionName), tostring(customDimensions.ControlId)
| project tostring(customDimensions.SubscriptionId), tostring(customDimensions.SubscriptionName),tostring(customDimensions.ResourceId), tostring(customDimensions.ControlId), oldresult =tostring(customDimensions.AttestationStatus)
| join
(
    customEvents
    | where timestamp >= now(-2d)
    | where  customDimensions.ScanSource =="Runbook" and name == "Control Scanned"
    | summarize arg_max(timestamp, *) by tostring(customDimensions.SubscriptionId), tostring(customDimensions.SubscriptionName), tostring(customDimensions.ControlId)
    | project tostring(customDimensions.SubscriptionId), tostring(customDimensions.SubscriptionName),tostring(customDimensions.ResourceId), tostring(customDimensions.ControlId), newresult = tostring(customDimensions.AttestationStatus)
)
on customDimensions_SubscriptionId, customDimensions_SubscriptionName,customDimensions_ResourceId, customDimensions_ControlId
| project tostring(customDimensions_SubscriptionId), tostring(customDimensions_SubscriptionName),tostring(customDimensions_ResourceId), tostring(customDimensions_ControlId),oldresult,newresult;

let oldScan = combinedResult
| summarize oldScanCount = count() by tostring(customDimensions_ControlId),oldresult;

let newScan = combinedResult
| summarize newScanCount = count() by tostring(customDimensions_ControlId),newresult;

oldScan
| join
(
    newScan
)
on customDimensions_ControlId,$left.oldresult==$right.newresult
| project ControlId=tostring(customDimensions_ControlId),AttestationStatus=oldresult,BeforeCount=oldScanCount,CurrentCount=newScanCount
| where BeforeCount != CurrentCount and AttestationStatus != "None"
| extend Drift =CurrentCount-BeforeCount
| order by Drift desc
| project ControlId, AttestationStatus, BeforeCount, CurrentCount,Drift
| project ControlId, AttestationStatus, BeforeCount, CurrentCount,Drift
```

**Below are the sample graphs generated by App Insights for above two queries.**

![AIGraph_FailednAttestation_Drift.jpg](/Images/06_AIGraph_FailednAttestation_Drift.jpg)
